apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
  compileSdkVersion rootProject.androidCompileSdkVersion
  buildToolsVersion rootProject.androidBuildToolsVersion

  defaultConfig {
    applicationId "com.edreams.android.workshops.kotlin"
    minSdkVersion rootProject.androidMinSdkVersion
    targetSdkVersion rootProject.androidTargetSdkVersion
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "com.edreams.android.workshops.kotlin.util.KotlinWorkshopJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    abortOnError false
  }

  sourceSets {
    String sharedTestDir = 'src/sharedTest/java'
    String sharedTestResDir = 'src/sharedTest/resources'
    test {
      resources {
        srcDir sharedTestResDir
      }
      java.srcDir sharedTestDir
    }
    androidTest {
      resources {
        srcDir sharedTestResDir
      }
      java.srcDir sharedTestDir
    }
  }
}

dependencies {
  def appDependencies = rootProject.ext.appDependencies
  def appTestDependencies = rootProject.ext.appTestDependencies

  implementation project(":injection")
  implementation project(":presentation")

  implementation appDependencies.supportV7
  implementation appDependencies.supportDesign
  implementation appDependencies.recyclerView
  implementation appDependencies.cardView
  implementation appDependencies.supportConstraint
  implementation appDependencies.kotlin
  implementation appDependencies.glide
  implementation appDependencies.dagger
  kapt appDependencies.daggerCompiler
  implementation appDependencies.daggerAndroid
  implementation appDependencies.daggerAndroidSupport
  kapt appDependencies.daggerAndroidCompiler
  implementation appDependencies.javaxInject
  implementation appDependencies.archExtensions

  testImplementation appTestDependencies.junit
  androidTestImplementation appTestDependencies.junit
  androidTestImplementation appTestDependencies.androidRunner
  androidTestImplementation appTestDependencies.androidRules
  androidTestImplementation appTestDependencies.mockitoAndroid
  androidTestImplementation(appTestDependencies.espressoCore, {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestImplementation(appTestDependencies.espressoContrib, {
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude module: 'recyclerview-v7'
  })
  kaptAndroidTest appTestDependencies.daggerCompiler
}
